apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {

    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "at.fhj.msd.stopwatch"
        minSdkVersion 24
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "int", "SPEEDFACTOR", "1"
    }
    //enable the generation of view binding classes
    buildFeatures {
        viewBinding true
    }

    //we need this (among other) to use `by viewModels()`
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    signingConfigs {
        release {
            String keys = file('../app/releaseKeystore.txt').getText('UTF-8')
            storeFile file("releaseKeystore.keystore")
            storePassword = keys
            keyAlias = 'keyReleaseApp'
            keyPassword = keys
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {

            buildConfigField "int", "SPEEDFACTOR", "4"
        }
    }

    flavorDimensions 'version'
    productFlavors {
        paid {
            dimension 'version'
            applicationIdSuffix '.paid'
        }
        free {
            dimension 'version'
            applicationIdSuffix '.free'
        }
    }
    sourceSets {
        paidRelease{
            res.srcDirs = ['src/paid/res', 'src/main/res']
        }
        paidDebug{
            res.srcDirs = ['src/paid/res', 'src/main/res']
        }
        freeRelease {
            res.srcDirs = ['src/free/res', 'src/main/res']
        }
        freeDebug{
            res.srcDirs = ['src/free/res', 'src/main/res']
        }
    }

}


dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    def lifecycle_version = "2.2.0"
    def nav_version = "2.3.1"

    //Fancy circular progress bar
    implementation 'com.mikhaellopez:circularprogressbar:3.0.3'


    //Navigation Graph
    //--------------------------------
    // Java language implementation
    implementation "androidx.navigation:navigation-fragment:$nav_version"
    implementation "androidx.navigation:navigation-ui:$nav_version"
    // Kotlin
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"


    //Architecture Components
    //--------------------------------
    // ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    // LiveData
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // Lifecycles only (without ViewModel or LiveData)
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    // Saved state module for ViewModel
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    //kotlin extension for easier fragment handling
    implementation "androidx.fragment:fragment-ktx:1.2.5"


    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.12'

    //test libraries for instrumented (device or emulator tests)
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'

    //Unit Testing extension and architecture component extensions
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    testImplementation "androidx.test.ext:junit-ktx:1.1.2"
    testImplementation "androidx.test:core-ktx:1.3.0"
    testImplementation "androidx.fragment:fragment-testing:1.2.5"
    testImplementation "org.robolectric:robolectric:4.4"

}